buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
        maven { url = "https://api.modrinth.com/maven"}
    }

    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath group: "org.spongepowered", name: "mixingradle", version: "0.7-SNAPSHOT"
    }

}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'
apply plugin: 'idea'
apply plugin: "org.spongepowered.mixin"

mixin {
    add sourceSets.main, "${project.modId}.refmap.json"
}

version = "${project.minecraftX}-${project.modVersion}"
group = 'com.mr_toad.h_plus'
archivesBaseName = project.modName

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {

    mappings channel: 'official', version: '1.19.4'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')


    runs {
        client {
            workingDirectory project.file('run')

            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"

            arg "-mixin.config=" + project.modId + ".mixins.json"

            mods {
                h_plus {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"

            arg "-mixin.config=" + project.modId + ".mixins.json"

            mods {
                h_plus {
                    source sourceSets.main
                }
            }
        }

        def dataArgs = ["--mod", project.modId, "--all", "--output", file("src/generated/resources/"), "--existing", file("src/main/resources")]

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            property "mixin.env.remapRefMap", "true"
            property "mixin.env.refMapRemappingFile", "${projectDir}/build/createSrgToMcp/output.srg"

            args dataArgs

            mods {
                h_plus {
                    source sourceSets.main
                }
            }
        }
    }
}



sourceSets.main.resources { srcDir 'src/generated/resources' }


dependencies {
    minecraft 'net.minecraftforge:forge:1.19.4-45.1.0'
    annotationProcessor "org.spongepowered:mixin:0.8.5:processor"

    implementation fg.deobf("maven.modrinth:toadlib:${project.toadlib}")
}


repositories {
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        forRepositories(fg.repository)
        filter {
            includeGroup "maven.modrinth"
        }
    }
}


jar {
    manifest {
        attributes([
                "Specification-Title"     : project.modId,
                "Specification-Vendor"    : project.modAuthor,
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.modName,
                "Implementation-Version"  : project.modVersion,
                "Implementation-Vendor"   : project.modAuthor,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : project.modId + ".mixins.json"
        ])
    }
}


jar.finalizedBy('reobfJar')

publishing {}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
